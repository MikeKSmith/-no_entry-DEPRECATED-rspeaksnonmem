$PROB WARFARIN PK
$INPUT ID TIME WT AGE SEX AMT DVID DV MDV
$DATA   warfarin_boot50.dta IGNORE=# 
 IGNORE=(DVID.EQ.2)
$SUBR
ADVAN2 TRANS2 
$PK
GRPCL = THETA(1)*(WT/70)**0.75
GRPV = THETA(2)*WT/70
GRPKA = THETA(3)
GRPLG = THETA(4)
CL = GRPCL*EXP(ETA(1)) 
V = GRPV*EXP(ETA(2)) 
KA = GRPKA*EXP(ETA(3)) 
ALAG1 = GRPLG*EXP(ETA(4)) 
S2 = V
$ERROR
CONC = A(2)/V
IPRED=F
IF(F.GT.0) IPRED=F
IRES = DV - IPRED
W = THETA(5)+THETA(6)*CONC
Y = IPRED + W*EPS(1)
$THETA (0.001, 0.1, ) ; POP_CL
(0.001, 8, ) ; POP_V
(0.001, 2, ) ; POP_KA
0.25 FIX ; POP_TLAG
0 FIX ; RUV_ADD
0.05 ; RUV_PROP
$OMEGA BLOCK(2) 0.1  	 ; PPV_CL
0.01 0.1 	 ; PPV_V

$OMEGA 0.1  ; PPV_KA 
0.0 FIX ; PPV_TLAG
$SIGMA 1 FIX 
$EST METHOD=COND INTER MAX=9990 SIG=3 NOABORT
$COV  

$TABLE ID TIME IPRED IRES RES PRED WRES APPEND ONEHEADER NOPRINT FILE=sdtab0
$TABLE ID CL V KA ALAG1 ETA(1) ETA(2) ETA(3) ETA(4) NOAPPEND ONEHEADER NOPRINT FILE=patab0
$TABLE ID AGE WT NOAPPEND ONEHEADER NOPRINT FILE=cotab0
$TABLE ID SEX NOAPPEND ONEHEADER NOPRINT FILE=catab0
