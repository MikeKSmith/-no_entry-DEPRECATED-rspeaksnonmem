$PROB THEOPHYLLINE POPULATION DATA
$INPUT ID DOSE TIME CP=DV WT
$DATA   'THEO'
$PRED
;THETA(1)=MEAN ABSORPTION RATE CONSTANT (1/HR)
;THETA(2)=MEAN ELIMINATION RATE CONSTANT (1/HR)
;THETA(3)=SLOPE OF CLEARANCE VS WEIGHT RELATIONSHIP (LITERS/HR/KG)
;DOSE=WT-ADJUSTED DOSE (MG/KG)
;DS=NON-WT-ADJUSTED DOSE (MG)
IF (DOSE.NE.0) THEN
DS=DOSE*WT
W=WT
ENDIF
KA=THETA(1)+ETA(1)
KE=THETA(2)+ETA(2)
CL=THETA(3)*W+ETA(3)
D=EXP(-KE*TIME)-EXP(-KA*TIME)
E=CL*(KA-KE)
F=DS*KE*KA/E*D
Y=F+EPS(1)
$THETA (0.1, 3, 5) ; A
(0.008, 0.08, 0.5) ; B
(0.004, 0.04, 0.9) ; C
$OMEGA BLOCK(3) 6   	 ; 1
0.005 2e-04  	 ; 2
0.3 0.006 0.4 	 ; 3

$SIGMA 0.4  
$EST MAXEVAL=450 PRINT=5
$COV  

$TABLE ID DOSE WT TIME APPEND ONEHEADER NOPRINT FILE=tab4.tab
